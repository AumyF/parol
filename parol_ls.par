%start ParolLs
%title "Parol grammar"
%comment "Parol's own grammar"
%line_comment "//"
%block_comment "/\*" "\*/"

%user_type OwnedToken = parol_runtime::lexer::OwnedToken


%%

ParolLs
    : Prolog GrammarDefinition
    ;

Prolog
    : StartDeclaration { Declaration } { ScannerState }
    ;

StartDeclaration
    : "%start": OwnedToken Identifier
    ;

Declaration
    : "%title": OwnedToken String
    | "%comment": OwnedToken String
    | "%user_type": OwnedToken Identifier "=": OwnedToken UserTypeName
    | ScannerDirectives
    ;

ScannerDirectives
    : "%line_comment": OwnedToken String
    | "%block_comment": OwnedToken String String
    | "%auto_newline_off": OwnedToken
    | "%auto_ws_off": OwnedToken
    ;

GrammarDefinition
    : "%%": OwnedToken Production { Production }
    ;

DoubleColon
    : "::": OwnedToken
    ;

Production
    : Identifier ":": OwnedToken Alternations ";": OwnedToken
    ;

Alternations
    : Alternation { "\|": OwnedToken Alternation }
    ;

Alternation
    : { Factor }
    ;

Factor
    : Group
    | Repeat
    | Optional
    | Symbol
    ;

Symbol
    : NonTerminal
    | SimpleToken
    | TokenWithStates
    | ScannerSwitch
    ;

SimpleToken
    : String [ASTControl]
    ;

TokenWithStates
    :  "<": OwnedToken StateList ">": OwnedToken String [ASTControl]
    ;

Group
    : "\(": OwnedToken Alternations "\)": OwnedToken
    ;

Optional
    : "\[": OwnedToken Alternations "\]": OwnedToken
    ;

Repeat
    : "\{": OwnedToken Alternations "\}": OwnedToken
    ;

NonTerminal
    : Identifier [ASTControl]
    ;

Identifier
    : "[a-zA-Z_][a-zA-Z0-9_]*": OwnedToken
    ;

String
    : "\u{22}([^\\]|\\.)*?\u{22}": OwnedToken
    ;

ScannerState
    : "%scanner": OwnedToken Identifier "\{": OwnedToken { ScannerDirectives } "\}": OwnedToken
    ;

StateList
    : Identifier { ",": OwnedToken Identifier }
    ;

ScannerSwitch
    : "%sc": OwnedToken "\(": OwnedToken [Identifier] "\)": OwnedToken
    | "%push": OwnedToken "\(": OwnedToken Identifier "\)": OwnedToken
    | "%pop": OwnedToken "\(": OwnedToken "\)": OwnedToken
    ;

ASTControl
    : CutOperator | UserTypeDeclaration
    ;

CutOperator
    : "\^": OwnedToken
    ;

UserTypeDeclaration
    : ":": OwnedToken UserTypeName
    ;

UserTypeName
    : Identifier { DoubleColon Identifier }
    ;
