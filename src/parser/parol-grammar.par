%start Grammar
%title "Parol grammar"
%comment "Parol's own grammar"
%line_comment "//"
%block_comment "/\*" "\*/"

%%

/*  0 */ Grammar: Prolog GrammarDefinition;
/*  1 */ Prolog: StartDeclaration {Declaration};
/*  3 */ StartDeclaration: "%start" Identifier;
/*  4 */ Declaration: "%title" String
/*  6 */    | "%comment" String
/*  7 */    | "%line_comment" String
/*  7 */    | "%block_comment" String String
/*  8 */    | "%auto_newline_off"
/*  9 */    | "%auto_ws_off";
/* 10 */ GrammarDefinition: "%%" Production {Production};
/* 11 */ Production: Identifier ":" Alternations ";";
/* 12 */ Alternations: Alternation {"\|" Alternation};
/* 13 */ Alternation: {Factor};
/* 14 */ Factor: Group
/* 15 */    | Repeat
/* 16 */    | Optional
/* 17 */    | Symbol;
/* 18 */ Symbol: Identifier
/* 19 */    | String;
/* 20 */ Group: "\(" Alternations "\)";
/* 21 */ Optional: "\[" Alternations "\]";
/* 22 */ Repeat: "\{" Alternations "\}";
/* 23 */ Identifier: "[a-zA-Z_]\w*";
/* 24 */ String: "\u{0022}([^\\]|\\.)*?\u{0022}";
